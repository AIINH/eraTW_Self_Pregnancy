;===========================================================
;冰之妖精的数据调整！
;用于UPDATE和初见。
;===========================================================
@CIRNO_UPDATE
SETCOLOR 0x51c4d3
$UPDATE_START
PRINTL
SELECTCASE RAND:10
	CASE 0
		PRINTFORML	❉❉❉(ᗜᴗᗜ)❉❉❉
	CASE 1
		PRINTFORML	❉❉❉(ᗜ_ᗜ)❉❉❉
	CASE 2
		PRINTFORML	❉❉❉(ᗜ ̮ᗜ)❉❉❉
	CASE 3
		PRINTFORML	❉❉❉(ᗜ‸ᗜ)❉❉❉
	CASEELSE
		PRINTFORML	❉❉❉(ᗜˬᗜ)❉❉❉
ENDSELECT
PRINTL
PRINTFORML	此处为琪露诺口上的调整界面。
PRINTFORML	本口上关于角色的性格和行为有许多个人的理解、且包含一定的暴力表现、还请见谅。
PRINTFORML	请选择需要调整的内容。
PRINTFORML	[1] 角色数据
PRINTFORML	[2] 暴力表现
PRINTFORML	[3] 关于此口上
PRINTFORML	[0] 没有了
INPUT
SELECTCASE RESULT
	CASE 1
		IF CFLAG:14:1000 != 1
			PRINTFORML	依照本口上的默认设置、琪露诺的素质与角色相性将发生变动。
		ELSE
			PRINTFORML	依照本口上的默认设置、琪露诺的素质与角色相性本应发生变动。
		ENDIF
		PRINTFORML	允许此变动吗？
		PRINTFORML	[1] 允许
		PRINTFORML	[2] 阻止（或还原）
		PRINTFORML	[3] 仅阻止涉及其他角色的部分
		SIF !CFLAG:14:面識
			PRINTFORML	[4] 保留（见面时决定）
		PRINTFORML	[0] 了解此变动
		INPUT
		SELECTCASE RESULT
			CASE 1
				TALENT:14:11 = -1
				TALENT:14:24 = 1
				RELATION:14:18 = 0
				RELATION:14:19 = 0
				RELATION:14:27 = 0
				RELATION:14:61 = 0
				RELATION:14:74 = 0
				RELATION:14:5 = 120
				RELATION:14:6 = 120
				RELATION:14:7 = 120
				RELATION:14:118 = 150
				RELATION:14:29 = 150
				RELATION:18:14 = 0
				RELATION:19:14 = 0
				RELATION:27:14 = 0
				RELATION:74:14 = 0
				RELATION:29:14 = 150
				RELATION:124:14 = 120
				RELATION:13:14 = 150
				PRINTFORML	已完成默认的素质与角色相性变动。
				PRINTL
			CASE 2
				IF CFLAG:14:1000 != 1
					PRINTFORML	不会改变琪露诺的素质与角色相性。
					PRINTFORMW	请宽容看待口上内容因此出现的少许矛盾。
					PRINTL
				ELSE
					TALENT:14:11 = 0
					TALENT:14:24 = 0
					RELATION:14:18 = 50
					RELATION:14:19 = 50
					RELATION:14:27 = 200
					RELATION:14:61 = 50
					RELATION:14:74 = 50
					RELATION:14:5 = 150
					RELATION:14:6 = 150
					RELATION:14:7 = 150
					RELATION:14:118 = 0
					RELATION:14:29 = 0
					RELATION:18:14 = 50
					RELATION:19:14 = 50
					RELATION:27:14 = 150
					RELATION:74:14 = 120
					RELATION:29:14 = 0
					RELATION:124:14 = 0
					RELATION:13:14 = 300
					PRINTFORML	已将本口上改变的素质与角色相性还原。
					CFLAG:14:1000 = 1
					PRINTL
				ENDIF
			CASE 3
				TALENT:14:11 = -1
				TALENT:14:24 = 1
				RELATION:14:18 = 0
				RELATION:14:19 = 0
				RELATION:14:27 = 0
				RELATION:14:61 = 0
				RELATION:14:74 = 0
				RELATION:14:5 = 120
				RELATION:14:6 = 120
				RELATION:14:7 = 120
				RELATION:14:118 = 150
				RELATION:14:29 = 150
				PRINTFORML	已完成不涉及其他角色的素质与角色相性变动。
				PRINTL
			CASE 4
				CFLAG:14:1000 = 3
				PRINTFORML	将等待到初见时再决定。
				PRINTFORML	如果已经见过面，则是保留之前的选择结果。
				PRINTL
			CASE 0
				PRINTFORML	出于情节安排的需要以及对角色的自我理解、本口上为琪露诺添加了坦率与开朗素质、并且将在取得恋人时更换傲慢素质。
				PRINTFORML	同时、调整了琪露诺与部分角色的相性如下：
				PRINTFORML	去除了莉莉白、莉莉黑、莉格露、妹红和蕾蒂的特殊相性（X→100）、这一变化是双向的；
				PRINTFORML	降低了对三月精的相性（150→120）、提升了对拉瓦尔的相性（100→150）、提升了对文的相性（100→150）；
				PRINTFORML	提升了文对琪露诺的相性（100→150）、提升了隐崎奈对琪露诺的相性（100→120）；
				PRINTFORML	以及、混杂了一点私心地、基于大妖精口上的内容倾向、降低了大妖精对琪露诺的相性（300→150）。
				PRINTFORML	以上为默认的变动内容。尽管相性这个数据似乎没有什么特别的作用、但、图个看着舒服吧大概。
				PRINTFORML	如果无法接受改变其他角色数据的行为，请选择阻止涉及其他角色的部分变动。
			CASE 9
				PRINTFORML 现在还没有隐藏内容、晚点再来看看吧……
		ENDSELECT
		GOTO UPDATE_START
	CASE 2
		PRINTFORML	所谓暴力表现、即是基于角色活泼好动性格、结合角色原作表现所决定的口上特性。
		PRINTFORML	一言以蔽之、就是傻子会打人。
		PRINTFORML	由于算法的限制、打得还挺疼、通常情况下造成的伤害可达900点体力。
		PRINTFORML	虽然这类表现并不多见、但可能有部分玩家依然不能适应。
		PRINTFORML	因此、这里给出一个将伤害降低到纯粹画风程度的选项。
		PRINTFORML	以及为保持一致、将同时关闭琪露诺的冷气效果。
		PRINTFORML	[0] 降低
		PRINTFORML	[1] 保持
		INPUT
		SELECTCASE RESULT
			CASE 0
				IF CFLAG:14:1001 == 0
					PRINTFORML	将降低造成的伤害。
					CFLAG:14:1001 = 1
				ELSE
					PRINTFORML	将依然降低造成的伤害。
				ENDIF
			CASE 1
				IF CFLAG:14:1001 == 0
					PRINTFORML	将依然造成正常数值的伤害。
				ELSE
					PRINTFORML	将重置为正常数值的伤害。
					CFLAG:14:1001 = 0
				ENDIF
		ENDSELECT
		GOTO UPDATE_START
	CASE 3
		PRINTFORM	首先鸣谢
		CALL COLORMESSAGE(" 花椰菜 ", 0xb6dc65, 0, 3)
		PRINTFORML	在各方面提供的一切帮助。
		PRINTFORML	（花椰菜是一名在4.842及之前版本活跃并成果丰富的口上汉化者。）
		PRINTFORML	（其较新的翻译作品包括典、辉夜、勇仪、正邪、露米娅等数十名角色。）
		PRINTFORML	（也曾制作过诸如白色情人节回礼等补丁。）
		PRINTFORML	
		PRINTFORML	此琪露诺口上由纸蛇冰沙制作，在一开始，主要是出于对「原有琪露诺口上竟只是大妖精扮演模式的挂件」的不满而动笔。
		PRINTFORML	在写作过程中，重新点燃了对角色的热情，于是才得以坚持至今。所以我认为写口上还是得为了喜欢这个角色才行。
		PRINTFORML	同时感谢以往在「制作口上教学频道」认识到并遗留下来的各位朋友一直以来的支持与鼓励。
		PRINTFORML	还有nosoma，一个写袿姬写一年写不出一个系的无名之辈，带我见识到这个游戏、从而得以开始。
		PRINTFORML	请无需期待。
		GOTO UPDATE_START
	CASE 0
		;关闭调整界面
		PRINTFORML	
	ENDSELECT
RESETCOLOR

;===========================================================
;冰之妖精的节日判断！
;用于作为节日的条件。
;ARG	0：问候	1：会话
;===========================================================
@CIRNO_FESTIVAL(ARG=0)
#FUNCTION
SELECTCASE FESTIVAL()
	CASE "元旦"
		RETURNF 1
	CASE "女兒節"
		SIF ARG
		RETURNF 1
	CASE "母親節"
	CASE "白色情人節前日"
	CASE "白色情人節"
	CASE "例大祭"
	CASE "兔子之日"
		SIF ARG
		RETURNF 1
	CASE "七夕"
		SIF !ARG
		RETURNF 1
	CASE "盂蘭盆節"
	CASE "風神「二百十日」"
	CASE "十五夜"
	CASE "収穫祭"
	CASE "萬聖節"
		SIF !ARG
		RETURNF 1
	CASE "節分"
	CASE "情人節"
		SIF !ARG
		RETURNF 1
	CASE "平安夜"
		SIF !ARG
		RETURNF 1
	CASE "聖誕節"
		SIF !ARG
		RETURNF 1
	CASE "大晦日"
		SIF !ARG
		RETURNF 1
ENDSELECT
RETURNF 0

;===========================================================
;冰之妖精的普通攻击！霜之哀伤吧大概。
;粗鲁的笨蛋就是会在因各种情况发火的时候会大打出手。但是，明明还是刮痧而已……
;到底是什么破口上会特地给攻击主角的行为写一个函数啊。
;ARG	0：初见	1：通常
;===========================================================
@CIRNO_ATTACK(ARG=1)
#DIM 伤害
#DIM DYNAMIC 威力 = 30
IF CFLAG:1001 == 1
	威力 = 1
ELSEIF MARK:反発刻印 >= 3
	威力 = 40
ENDIF

;战斗能力的等级差每级提供150的减伤。如果结果≤0，则视为闪避成功。如果结果≤-300，则视为轻松应对。
;以下的估算伤害值均为主角战力0级且⑨战力1级的初始情况。按照TW的能力提升机制，这个战力差应该不会再增加，只会减少。
SETCOLOR 0x51c4d3
IF ARG == 0
	;150~270
	伤害 = 威力 * (5*(ABL:戦闘能力 - ABL:MASTER:戦闘能力) + MAX(RAND:(4), 0))
	IF 伤害 <= 0
		PRINTFORMW	堪堪擦过了抛掷过来的冰块、姑且还是没有什么大碍……
		PRINTFORML	「呀、打歪啦！……唔、不攻过来吗？」
		PRINTFORMDL	似乎是想擅自挑起弹幕胜负的样子、但这种程度对付%CALLNAME:MASTER%只会被瞬间击溃吧。
	ELSE
		CALL RECOVER(MASTER, -伤害, "体力", "琪露诺的冰锥")
		PRINTFORMDW	被抛掷过来的冰块打了个正着、虽然没有流血但还是很痛啊——
		PRINTFORML	「咦、你不会打架的吗？真没趣——」
		PRINTFORMDL	似乎被以一种非常丢脸的方式放过了……
	ENDIF
ELSEIF TALENT:愛欲
	;90~300
	伤害 = 威力 * (5*(ABL:戦闘能力 - ABL:MASTER:戦闘能力) + MAX(RAND:(10), 3))
	IF 伤害 <= -300
		PRINTFORMW	轻松地躲开了整片寒雾……
	ELSEIF 伤害 <= 0
		PRINTFORMW	勉强躲开了寒雾的攻击……
	ELSE
		CALL RECOVER(MASTER, -伤害, "体力", "琪露诺的寒雾")
	ENDIF
ELSEIF CFLAG:好感度 >= 500 || BASE:10 >= 1000
	;270~600
	伤害 = 威力 * (5*(ABL:戦闘能力 - ABL:MASTER:戦闘能力) + MAX(RAND:(20), 9))
	IF 伤害 <= -300
		PRINTFORMW	轻松地躲开了所有碎冰……
	ELSEIF 伤害 <= 0
		PRINTFORMW	勉强躲开了碎冰的攻击……
	ELSE
		CALL RECOVER(MASTER, -伤害, "体力", "琪露诺的碎冰")
	ENDIF
ELSE
	;450~900
	伤害 = 威力 * (5*(ABL:戦闘能力 - ABL:MASTER:戦闘能力) + MAX(RAND:(30), 15))
	IF 伤害 <= 0
		PRINTFORMW	勉强躲开了冰锥的攻击……
	ELSE
		CALL RECOVER(MASTER, -伤害, "体力", "琪露诺的冰锥")
	ENDIF
ENDIF
SETCOLOR 0x51dede
	
;===========================================================
;冰之妖精的入梦！
;和冰冰凉凉的小家伙睡在一块，就跟开满空调睡一样。夏天很爽，冬天就遭殃了……
;===========================================================
@CIRNO_SLEEP
SETCOLOR 0x51c4d3
PRINTL
SELECTCASE DAY:2
	CASE 4
		IF TALENT:恋慕
			PRINTFORMW 虽然琪露诺稍微收敛了冷气、但还是有些不太适应……
			CALL RECOVER(MASTER,-900,"体力")
			CALL RECOVER(MASTER,-900,"気力")
		ELSE
			PRINTFORMW 身边冷气萦绕、感觉实在是睡不着觉……
			CFLAG:MASTER:徹夜 = 1
		ENDIF
	CASE 2
		PRINTFORMW 在琪露诺身旁睡着、感觉很是凉爽……
		CALL RECOVER(MASTER,900,"体力")
		CALL RECOVER(MASTER,900,"気力")
	CASEELSE
		IF !TALENT:恋慕 && RAND:2 == 0
			PRINTFORMW 身边冷气萦绕、感觉有点难以入睡……
			CALL RECOVER(MASTER,-900,"体力")
			CALL RECOVER(MASTER,-900,"気力")
		ENDIF
ENDSELECT
SETCOLOR 0x51dede
		
;===========================================================
;冰之妖精的冷气！
;真正的招牌技能，恒定施放的冰冻光环。
;在各种会引发肢体碰撞的指令调用。
;ARG:0表示触发层数。自然是越激烈的接触越多。
;开空调，可以不可以？
;===========================================================
@CIRNO_FREEZE(ARG:0)
#DIM 冷气

IF FIRSTTIME("冷气")
	SETCOLOR 0x51c4d3
	PRINTL 
	PRINTFORMDL	就如同刚见面时的感觉一般、这个冰之妖精会在身边散发出强烈的冷气。
	PRINTFORMDL	尽管她自己大概只是无意识的举动、但对于人类而言就……
	PRINTFORMDL	恐怕会需要一些时间来适应。或许、如果足够亲近、也能让她稍微注意一点……
	SETCOLOR 0x51dede
ENDIF
SIF TCVAR:351 == 0
	TCVAR:351 += CFLAG:1010
	
;陷落决定基础伤害
IF TALENT:恋慕 && TALENT:恋人 && CFLAG:1019
	冷气 = 0
ELSEIF TALENT:恋慕
	冷气 = -30
ELSEIF TALENT:思慕
	冷气 = -60
ELSE
	冷气 = -90
ENDIF
;坏心情加伤
SIF TALENT:15 == -1
	冷气 -= 30
;反发加伤
SELECTCASE MARK:反発刻印
	CASE 1
		冷气 -= 30
	CASE 2
		冷气 -= 60
	CASE 3
		冷气 -= 90
ENDSELECT
;好感度减伤
SELECTCASE CFLAG:2
	CASE IS >= 256000
		冷气 += 20
	CASE IS >= 128000
		冷气 += 18
	CASE IS >= 64000
		冷气 += 16
	CASE IS >= 32000
		冷气 += 14
	CASE IS >= 16000
		冷气 += 12
	CASE IS >= 8000
		冷气 += 10
	CASE IS >= 4000
		冷气 += 8
	CASE IS >= 2000
		冷气 += 6
	CASE IS >= 1000
		冷气 += 4
	CASE IS >= 500
		冷气 += 2
ENDSELECT
;信赖度减伤
SELECTCASE CFLAG:4
	CASE IS >= 51200
		冷气 += 20
	CASE IS >= 25600
		冷气 += 18
	CASE IS >= 12800
		冷气 += 16
	CASE IS >= 6400
		冷气 += 14
	CASE IS >= 3200
		冷气 += 12
	CASE IS >= 1600
		冷气 += 10
	CASE IS >= 800
		冷气 += 8
	CASE IS >= 400
		冷气 += 6
	CASE IS >= 200
		冷气 += 4
	CASE IS >= 100
		冷气 += 2
ENDSELECT
;亲密减伤
SELECTCASE ABL:親密
	CASE IS >= 60
		冷气 += 30
	CASE IS >= 50
		冷气 += 27
	CASE IS >= 40
		冷气 += 24
	CASE IS >= 30
		冷气 += 21
	CASE IS >= 25
		冷气 += 18
	CASE IS >= 20
		冷气 += 15
	CASE IS >= 15
		冷气 += 12
	CASE IS >= 9
		冷气 += 9
	CASE IS >= 6
		冷气 += 6
	CASE IS >= 3
		冷气 += 3
ENDSELECT
;爱情经验减伤
SELECTCASE EXP:愛情経験
	CASE IS >= 1000
		冷气 += 30
	CASE IS >= 600
		冷气 += 27
	CASE IS >= 400
		冷气 += 24
	CASE IS >= 250
		冷气 += 21
	CASE IS >= 150
		冷气 += 18
	CASE IS >= 100
		冷气 += 15
	CASE IS >= 70
		冷气 += 12
	CASE IS >= 45
		冷气 += 9
	CASE IS >= 25
		冷气 += 6
	CASE IS >= 10
		冷气 += 3
ENDSELECT
;同日耐受度减伤
SELECTCASE TCVAR:350
	CASE IS >= 900
		冷气 += 50
	CASE IS >= 660
		冷气 += 45
	CASE IS >= 490
		冷气 += 40
	CASE IS >= 340
		冷气 += 35
	CASE IS >= 220
		冷气 += 30
	CASE IS >= 120
		冷气 += 25
	CASE IS >= 70
		冷气 += 20
	CASE IS >= 35
		冷气 += 15
	CASE IS >= 15
		冷气 += 10
	CASE IS >= 3
		冷气 += 5
ENDSELECT
;夏天减伤，冬天加伤
IF DAY:2 == 2
	冷气 += 30
ELSEIF DAY:2 == 4
	冷气 -= 30
ENDIF
;当前指令为泡澡时减伤
SIF SELECTCOM == 431
	冷气 += 30
;当前地图为地底时减伤
SIF GET_MAPID(CFLAG:MASTER:現在位置) == 9
	冷气 += 30
;御寒心法（？
IF FIRSTTIME("御寒第一重") && TCVAR:350 >= 10
	CFLAG:1010 += 2
ELSEIF FIRSTTIME("御寒第二重") && TCVAR:350 >= 25
	CFLAG:1010 += 4
ELSEIF FIRSTTIME("御寒第三重") && TCVAR:350 >= 50
	CFLAG:1010 += 6
ELSEIF FIRSTTIME("御寒第四重") && TCVAR:350 >= 100
	CFLAG:1010 += 8
ELSEIF FIRSTTIME("御寒第五重") && TCVAR:350 >= 150
	CFLAG:1010 += 10
ELSEIF FIRSTTIME("御寒第六重") && TCVAR:350 >= 200
	CFLAG:1010 += 12
ELSEIF FIRSTTIME("御寒第七重") && TCVAR:350 >= 300
	CFLAG:1010 += 14
ELSEIF FIRSTTIME("御寒第八重") && TCVAR:350 >= 400
	CFLAG:1010 += 16
ELSEIF FIRSTTIME("御寒第九重") && TCVAR:350 >= 550
	CFLAG:1010 += 18
ENDIF

;结果处理
IF CFLAG:1001 != 1
	REPEAT ARG:0
		IF TALENT:恋慕 && TALENT:恋人 && 冷气 >= 0
			冷气 /= 9
			CALL BUFF_BASE(MASTER,BASE_気力,冷气)
		ELSEIF 冷气 < 0
			SIF TCVAR:発情
				冷气 /= 2
			CALL BUFF_BASE(MASTER,BASE_気力,冷气)
			TCVAR:350 += ARG:0
			TCVAR:351 -= 冷气
		ELSE
			冷气 = 0
		ENDIF
	REND
	SETCOLOR 0x51c4d3
	PRINTL 
	SELECTCASE TCVAR:351
		CASE IS >= 4000
			PRINTFORML	呼吸好艰难、眼前的东西也变得模糊了……
		CASE IS >= 3000
			PRINTFORML	明明很冷、但身体却感觉开始发热了、而且好痛……
		CASE IS >= 2000
			PRINTFORML	皮肤冷得发麻、关节好像都要冻僵了……
		CASE IS >= 500
			PRINTFORML	和琪露诺接触的时候、感觉冷意在往体内蔓延……
		CASE IS >= 0
			PRINTFORML	冰之妖精的身边似乎时刻充满冷气……
		CASEELSE
			PRINTFORML	从琪露诺的方向有着清爽的凉风吹来……
	ENDSELECT
	SETCOLOR 0x51dede
ENDIF

;===========================================================
;冰之妖精的嫉妒！
;大概是随机会抽取一个恋人。目前仅用于猜猜爷是谁。
;在使用时DIM一个变量ENVYTOY然后赋值为此函数，再%CALLNAME:ENVYTOY%就行了。
;===========================================================
@CIRNO_ENVY()
#FUNCTION
#LOCALSSIZE 10000
VARSET LOCALS

FOR LOCAL, 0, CHARANUM
    SIF CFLAG:LOCAL:2 < 1000 || NO:LOCAL == 0 || NO:LOCAL == 14
		CONTINUE
    IF TALENT:LOCAL:恋人
        SIF LOCALS != ""
            LOCALS += " "
		LOCALS += TOSTR(LOCAL)
    ENDIF
NEXT

SPLIT LOCALS, " ", LOCALS

SIF RESULT
	RETURNF TOINT(LOCALS:(RAND:RESULT))
RETURNF -1
	
;===========================================================
;冰之妖精的绝顶！
;计算当前事件的合计绝顶数。
;用于推倒结束时。
;===========================================================
@CIRNO_CLIMAX()
#FUNCTION
#DIM DYNAMIC 绝顶合计次数 = 0
绝顶合计次数 += EX:14:Ｃ絶頂
绝顶合计次数 += EX:14:Ｖ絶頂
绝顶合计次数 += EX:14:Ａ絶頂
绝顶合计次数 += EX:14:Ｂ絶頂
绝顶合计次数 += EX:14:Ｍ絶頂
RETURNF 绝顶合计次数
	
;===========================================================
;冰之妖精的突击检查！
;没收胖次时检测是否有自己的胖次
;在原事件里声明变量：
;#DIM DYNAMIC iPants_Num
;iPants_Num = 0
;之后用以下语句分别赋值和判断。
;	iPants_Num = CIRNO_CHECKPANTS(TARGET)
;	IF iPants_Num
;===========================================================
@CIRNO_CHECKPANTS(iID=14)
#FUNCTION
#DIM iID
#DIM iPants_Type
#DIM iPants_Num

;パンツ輸送
SIF !FLAG:853
	RETURNF 0

iPants_Num = 0
FOR iPants_Type,0,MAXPANTS
	SIF PANTS_TEMP:iID:iPants_Type
		iPants_Num += PANTS_TEMP:iID:iPants_Type
NEXT
RETURNF iPants_Num

;==================================================
;冰之妖精的在场观众！
;从典口上借的，但有过修改。大概就是从同一位置的其他人里选取一个，优先度从关联角色到全随机。
;原本来说是优先度高的情况会直接决定结果，但为了自家想要的效果，改成了优先度仅影响概率。
;输出结果是角色编号，如果没有人的话就是0了。
;-------------------------------------------------
@CIRNO_FIND_AROUND()
#FUNCTION
#DIM 关联角色,14 = 1, 5, 6, 7, 11, 13, 29, 30, 33, 49, 50, 68, 118, 124
#DIM 总人数
#DIM 抽选
#LOCALSIZE 200
;关联角色包括：灵梦、桑尼、露娜、斯塔、魔理沙、大妖精、文、映姬、诹访子、觉、芙兰朵露、幽香、拉尔瓦、隐岐奈。
VARSET LOCAL
抽选 = 0
总人数 = GET_TARGETNUM() 
;如果没人的话就是0。
SIF 总人数 == 1
	RETURNF 0
;首先将所有在场的角色总结成一个权重池。
FOR COUNT,1, 总人数 + 1
	;屏蔽抽中口上主的情况。
	SIF TARGET:COUNT == TARGET:0
		CONTINUE
	;确认目标是否与主角和口上主在同一位置。
	SIF (CFLAG:(TARGET:COUNT):現在位置 != CFLAG:MASTER:現在位置) || (CFLAG:(TARGET:COUNT):現在位置 != CFLAG:14:現在位置)
		CONTINUE
	;如果上述条件都是满足的，那么将此角色编号加入权重池。
	LOCAL:抽选 = TARGET:COUNT
	抽选 ++
NEXT
;然后将熟人额外加入一次权重池，也就是概率翻倍了。
FOR COUNT, 0, 14
	IF CFLAG:(关联角色:COUNT):現在位置 == CFLAG:MASTER:現在位置
		LOCAL:抽选 = 关联角色:COUNT
		抽选 ++
	ENDIF
NEXT
;不知道为什么，但总之有这么一个结尾。
SIF 抽选 < 1
	RETURNF 0
;最后在池子里随机抽一个啦。
RETURNF LOCAL:(RAND(抽选))

;===========================================================
;冰之妖精的量子坍缩！（？
;条件满足越多概率越高吧大概。
;使用时ARG默认输入1，后面的ARG:1到ARG:9都是条件（包括且不仅限于TALENT，CFLAG，TCVAR，ABL，EXP等）
;目前用于节日遭遇。
;===========================================================
@CIRNO_RAND(ARG = 0, ARG:1, ARG:2, ARG:3, ARG:4, ARG:5, ARG:6, ARG:7, ARG:8, ARG:9)
#FUNCTION
#DIM LCOUNT

IF ARG == 1
	LOCAL = 11
	FOR LCOUNT, 1, 10
		SIF ARG:LCOUNT > 0
			LOCAL -= 1
	NEXT
ENDIF
SIF RAND:LOCAL == 0
	RETURNF 1
RETURNF 0
